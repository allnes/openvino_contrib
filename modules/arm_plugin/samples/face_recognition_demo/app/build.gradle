plugins {
    id 'com.android.application'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.openvino_demo.face_recognition_demo"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation project(':opencv')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "androidx.camera:camera-camera2:1.0.0"
    implementation "androidx.camera:camera-lifecycle:1.0.0"
    implementation "androidx.camera:camera-view:1.0.0-alpha25"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation fileTree(dir: '/home/anesterov/android_ov/opencv-4.5.0-android-sdk/OpenCV-android-sdk/sdk/')
}

task copyOPVLibs(type: Copy) {
    from ("$System.env.OPENVINO_ARM_LIBRARY_PATH/") { include '**/*.so' }
    into "$projectDir/src/main/jniLibs/$System.env.ARM_PROCESSOR_CONFIGURATION/"
}
preBuild.dependsOn(copyOPVLibs)

task copyAndroidLib(type: Copy) {

    from "$System.env.ANDROID_NDK_LIBRARY_PATH/sources/cxx-stl/llvm-libc++/libs/$System.env.ARM_PROCESSOR_CONFIGURATION/libc++_shared.so"
    into "$projectDir/src/main/jniLibs/$System.env.ARM_PROCESSOR_CONFIGURATION/"
}
preBuild.dependsOn(copyAndroidLib)

task copyOPVJarFiles(type: Copy) {
    from("$System.env.OPENVINO_ARM_LIBRARY_PATH/") { include '**/*.jar' }
    into "$projectDir/libs/"
}
preBuild.dependsOn(copyOPVJarFiles)

task copyOMZNetworks(type: Copy) {
    from("$System.env.OMZ_NETWORKS_PATH/") { include '**/*.xml', '**/*.bin' }
    into "$projectDir/src/main/assets/"
}
preBuild.dependsOn(copyOMZNetworks)

task copyPluginXML(type: Copy) {
    from("$System.env.OPENVINO_ARM_LIBRARY_PATH/") { include '**/*.xml' }
    into "$projectDir/src/main/assets/"
}
preBuild.dependsOn(copyPluginXML)